-- Defines the statement that a given Sudoku problem has a solution.

data Value ≅ Fin(9).
data Row ≅ Fin(9).
data Col ≅ Fin(9).
data Cell ≅ Row × Col.
data Problem ≅ Cell →^81 Maybe(Value).
data Solution ≅ Cell →^81 Value.
data Square ≅ Fin(3) × Fin(3).
data SquareCell ≅ Fin(3) × Fin(3).

def getCell : Square → SquareCell → Cell
  := λs : Square ⇨ λc : SquareCell
  ⇨ let s' : Fin(3) × Fin(3) := from(Square)(s);
    let c' : Fin(3) × Fin(3) := from(SquareCell)(c);
    to(Cell)(( to(Row)(cast((3N ×ℕ cast(π1(s')))
                 +ℕ cast(π1(c'))))
             , to(Col)(cast((3N ×ℕ cast(π2(s')))
                 +ℕ cast(π2(c')))) )).

def solutionIsWellFormed : Solution → Prop
  := λs : Solution
  ⇨ let f : Cell → Value := from(Solution)(s);
     (∀r : Row, ∀v : Value, ∃c : Col, f(to(Cell)((r,c))) = v)
   ∧ (∀c : Col, ∀v : Value, ∃r : Row, f(to(Cell)((r,c))) = v)
   ∧ (∀r : Square, ∀v : Value, ∃c : SquareCell, f(getCell(r,c)) = v).

def solutionMatchesProblem : Problem → Solution → Prop
  := λp : Problem ⇨ λs : Solution
  ⇨ let f : Cell → Maybe(Value) := from(Problem)(p);
    let g : Cell → Value := from(Solution)(s);
    ∀c : Cell, ((f(c) = nothing) ∨ (f(c) = just(g(c)))).

def problemIsSolvable : Problem → Prop
  := λp : Problem ⇨ ∃s : Solution,
     solutionMatchesProblem(p, s) ∧ solutionIsWellFormed(s).
